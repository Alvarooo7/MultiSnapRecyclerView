// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.configs = [
      groupId: "com.github.takusemba",
      artifactId: "multisnaprecyclerview",

      libraryVersionCode: 13,
      libraryVersionName: "1.3.4",

      compileSdkVersion: 25,
      minSdkVersion: 13,
      targetSdkVersion: 25,
  ]
  ext.versions = [
      kotlin: '1.3.11',
      androidx: '25.3.1',
  ]
  ext.deps = [
      androidPlugin: "com.android.tools.build:gradle:3.5.1",
      bintrayPlugin: "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4",
      kotlin: [
          plugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          stdlib: "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
      ],
      androidx: [
          appcompat: "com.android.support:appcompat-v7:${versions.androidx}",
          recyclerview: "com.android.support:recyclerview-v7:${versions.androidx}",
          cardview: "com.android.support:cardview-v7:${versions.androidx}",
      ],
  ]
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath deps.androidPlugin
    classpath deps.bintrayPlugin
    classpath deps.kotlin.plugin
  }
}

allprojects {
  repositories {
    jcenter()
    google()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
